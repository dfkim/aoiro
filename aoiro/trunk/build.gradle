apply plugin: 'java'
apply plugin: 'eclipse'

version = '0.9'

tasks.withType(JavaCompile) {
	sourceCompatibility = 11
	targetCompatibility = 11
	options.compilerArgs << '-Xlint:all'
}

repositories {
    jcenter()
}

dependencies {
	implementation fileTree(dir: 'lib',
		includes: ['**/*.jar'],
		excludes: ['**/*-sources.jar', '**/*-javadoc.jar'])

	implementation 'org.apache.pdfbox:pdfbox:2.0.22'
	implementation 'org.apache.pdfbox:fontbox:2.0.22'

	implementation 'net.java.dev.jna:jna:5.6.0'
	implementation 'net.java.dev.jna:jna-platform:5.6.0'

	implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.15'
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources', 'src/main/java' ]
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

defaultTasks 'clean', 'build', 'exewrap'

jar {
	manifest {
		attributes "Specification-Version": "${project.version}"
		attributes "Main-Class" : "net.osdn.aoiro.cui.Main"
		attributes "Class-Path": sourceSets.main.runtimeClasspath.collect { "lib/$it.name" }.join(' ')
	}
}

task copyLibraries(type: Copy) {
	setGroup("build")
	setDescription("Copy dependency libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/package/lib/"
}

task createJavaRuntime(type: Exec, dependsOn: ['jar', 'copyLibraries']) {
	setGroup("build")
	setDescription("Create Java Runtime.")
	workingDir "${buildDir}/package/"
	commandLine 'cmd', '/c', 'CreateJRE.bat', "${jar.archiveFile.get()}", "lib", "-server"
}

task exewrap(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/etc/exewrap.exe"
	args "-A", "x64",
		"-t", "11",
		"-a", "-Xms256m -Xmx1024m",
		"-e", "NOLOG",
		"-j", "${jar.archiveFile.get()}",
		"-i", "etc/ico/app.ico",
		"-o", "${buildDir}/package/${archivesBaseName}.exe",
		"-p", "aoiro",
		"-d", "aoiro",
		"-c", "(C) 2017-2020 HIRUKAWA Ryo",
		"-V", "${version}",
		"-v", "${version}"
}

task buildAll(dependsOn: ['createJavaRuntime', 'copyLibraries', 'exewrap', 'clean']) {
	setGroup("build")
}
